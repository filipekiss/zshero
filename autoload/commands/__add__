#!/usr/bin/env zsh

__zshero::base "utils/bin"
__zshero::base "config/*"
__zshero::base "io/*"
__zshero::utils::bin::check "git" \
    "You need git! Install git and try again!" || return $status

local CURRENT_FOLDER=$(__zshero::core::root_folder)

local config_name="$1"
shift
requested_files=("${argv[@]:A}")
typeset -aU new_files new_folders
for file in $requested_files; do
    if __zshero::config::config::check_file $file; then
        new_files+=($file)
    fi
    if __zshero::io::is::folder $file; then
        new_folders+=($file)
    fi
done;

__zshero::config::config::validate_namespace $config_name

for folder in $new_folders; do
    result=$(__zshero::config::config::from_folder $folder)
    for file_path in ${(@f)result}; do
        if __zshero::config::config::check_file $file_path; then
            new_files+=($file_path)
        fi
    done;
done


for file in $new_files; do
  __zshero::config::config::add_file_to_config $config_name $file
done

[[ -n $new_files ]] && __zshero::config::adopt $config_name || __zshero::io::print::warn "No files to add!"
